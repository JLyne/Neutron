import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'io.freefair.lombok' version '5.3.3.3'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'maven-publish'
	id 'java'
}

repositories {
	mavenLocal()
	mavenCentral()

	maven {
        url 'https://repo.velocitypowered.com/releases/'
    }
    
    maven {
        url 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
	implementation 'org.spongepowered:configurate-hocon:3.6'
	
	compileOnly 'com.velocitypowered:velocity-api:3.0.0'
	annotationProcessor 'com.velocitypowered:velocity-api:3.0.0'
}

group 'uk.co.notnull'
version '3.0.0-SNAPSHOT'
description 'An open-source essential suite for the Velocity proxy'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }

    shadowJar {
        archiveClassifier.set('')
    }

    build {
        dependsOn(shadowJar)
    }
}
